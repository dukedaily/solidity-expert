# Solidityè¯­æ³•ï¼ˆä¸‹ï¼‰

> å°ç™½å…¥é—¨ï¼šhttps://github.com/dukedaily/solidity-expert ï¼Œæ¬¢è¿starè½¬å‘ï¼Œæ–‡æœ«åŠ Vå…¥ç¾¤ã€‚
>
> èŒåœºè¿›é˜¶: https://dukeweb3.com

## è®²å¸ˆä»‹ç»

èµ„æ·±web3å¼€å‘è€…ï¼Œbybitäº¤æ˜“æ‰€defiå›¢é˜ŸTech Leadï¼ŒMoleDAOæŠ€æœ¯é¡¾é—®ï¼Œå›½å†…ç¬¬ä¸€æ‰¹åŒºå—é“¾å¸ƒé“è€…ï¼Œä¸“æ³¨æµ·å¤–defi,dex,AAé’±åŒ…ç­‰ä¸šåŠ¡æ–¹å‘ã€‚

- å…¬ä¼—å·ï¼š[é˜¿æœåœ¨æ–°åŠ å¡](https://mp.weixin.qq.com/mp/appmsgalbum?__biz=MzU5NDQ0NDAxNQ==&action=getalbum&album_id=2529739108240556033&scene=173&from_msgid=2247484601&from_itemidx=1&count=3&nolastread=1#wechat_redirect) 
- githubï¼š[ä»¥å¤ªåŠæ•™ç¨‹](https://github.com/dukedaily)
- Bç«™ï¼š[æœæ—­duke](https://www.bilibili.com/video/BV1EY4y1c7Yq/?vd_source=42fe91bf6d16ec8841b22ea520184d76)
- Youtubeï¼š[duke du](https://www.youtube.com/watch?v=Wpf5KkgzElc&list=PLO_KaIZjoik9oY-Rs9BsDkHY2RJy7WcE-)
- Twitterï¼š[dukedu2022](https://twitter.com/home)



## interface

å¯ä»¥ä½¿ç”¨Interfaceå®Œæˆå¤šä¸ªåˆçº¦ä¹‹é—´è¿›è¡Œäº¤äº’ï¼Œinterfaceæœ‰å¦‚ä¸‹ç‰¹æ€§ï¼š

1. æ¥å£ä¸­å®šä¹‰çš„functionä¸èƒ½å­˜åœ¨å…·ä½“å®ç°ï¼›
2. æ¥å£å¯ä»¥ç»§æ‰¿ï¼›
3. æ‰€æœ‰çš„functionå¿…é¡»å®šä¹‰ä¸ºexternalï¼›publicï¼Œinternalï¼Œprivate
4. æ¥å£ä¸­ä¸èƒ½å­˜åœ¨constructorå‡½æ•°ï¼›
5. æ¥å£ä¸­ä¸èƒ½å®šä¹‰çŠ¶æ€å˜é‡ï¼›
6. [abstractå’Œinterfaceçš„åŒºåˆ«](https://medium.com/upstate-interactive/solidity-how-to-know-when-to-use-abstract-contracts-vs-interfaces-874cab860c56)

```js
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.13;

contract Counter {
    uint public count;

    function increment() external {
        count += 1;
    }
}

interface IBase {
    function count() external view returns (uint);
}

interface ICounter is IBase {
  	// uint num;
    function increment() external;
}

contract MyContract {
    function incrementCounter(address _counter) external {
        ICounter(_counter).increment();
    }

    function getCount(address _counter) external view returns (uint) {
        return ICounter(_counter).count();
    }
}
```

uniswap demo:

```js
// Uniswap example
interface UniswapV2Factory {
    function getPair(address tokenA, address tokenB)
        external
        view
        returns (address pair);
}

interface UniswapV2Pair {
    function getReserves()
        external
        view
        returns (
            uint112 reserve0,
            uint112 reserve1,
            uint32 blockTimestampLast
        );
}

contract UniswapExample {
    address private factory = 0x5C69bEe701ef814a2B6a3EDD4B1652CB9cc5aA6f;
    address private dai = 0x6B175474E89094C44Da98b954EedeAC495271d0F;
    address private weth = 0xC02aaA39b223FE8D0A0e5C4F27eAD9083C756Cc2;

    function getTokenReserves() external view returns (uint, uint) {
        address pair = UniswapV2Factory(factory).getPair(dai, weth);
        (uint reserve0, uint reserve1, ) = UniswapV2Pair(pair).getReserves();
        return (reserve0, reserve1);
    }
}
```

## library

åº“ä¸åˆçº¦ç±»ä¼¼ï¼Œ**é™åˆ¶ï¼šä¸èƒ½åœ¨åº“ä¸­å®šä¹‰çŠ¶æ€å˜é‡ï¼Œä¸èƒ½å‘åº“åœ°å€ä¸­è½¬å…¥ether**ï¼Œåº“æœ‰ä¸¤ç§å­˜åœ¨å½¢å¼ï¼š

1. å†…åµŒï¼ˆembeddedï¼‰ï¼šå½“åº“ä¸­æ‰€æœ‰çš„æ–¹æ³•éƒ½æ˜¯internalæ—¶ï¼Œæ­¤æ—¶ä¼šå°†åº“ä»£ç å†…åµŒåœ¨è°ƒç”¨åˆçº¦ä¸­ï¼Œä¸ä¼šå•ç‹¬éƒ¨ç½²åº“åˆçº¦ï¼›
2. ==é“¾æ¥ï¼ˆlinkedï¼‰==ï¼šå½“åº“ä¸­å«æœ‰externalæˆ–publicæ–¹æ³•æ—¶ï¼Œæ­¤æ—¶ä¼šå•ç‹¬å°†åº“åˆçº¦éƒ¨ç½²ï¼Œå¹¶åœ¨è°ƒç”¨åˆçº¦éƒ¨ç½²æ—¶é“¾æ¥linkåˆ°åº“åˆçº¦ã€‚
   1. å¯ä»¥å¤ç”¨çš„ä»£ç å¯ä»¥ç¼–å†™åˆ°åº“ä¸­ï¼Œä¸åŒçš„è°ƒç”¨è€…å¯ä»¥linkedåˆ°ç›¸åŒçš„åº“ï¼Œå› æ­¤ä¼šæ›´åŠ èŠ‚çº¦gasï¼›
   2. å¯¹äºlinkedåº“åˆçº¦ï¼Œè°ƒç”¨åˆçº¦ä½¿ç”¨delegatecallè¿›è¡Œè°ƒç”¨ï¼Œæ‰€ä»¥ä¸Šä¸‹æ–‡ä¸ºè°ƒç”¨åˆçº¦ï¼›
   3. éƒ¨ç½²å·¥å…·ï¼ˆå¦‚remixï¼‰ä¼šå¸®æˆ‘ä»¬è‡ªåŠ¨éƒ¨ç½²&é“¾æ¥åˆçº¦åº“ã€‚

```js
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.13;

// 1. åªæœ‰internalæ–¹æ³•ï¼Œä¼šå†…åµŒåˆ°è°ƒç”¨åˆçº¦ä¸­
library SafeMath {
  
    function add(uint x, uint y) internal pure returns (uint) {
        uint z = x + y;
        require(z >= x, "uint overflow");

        return z;
    }
}

library Math {
    function sqrt(uint y) internal pure returns (uint z) {
        if (y > 3) {
            z = y;
            uint x = y / 2 + 1;
            while (x < z) {
                z = x;
                x = (y / x + x) / 2;
            }
        } else if (y != 0) {
            z = 1;
        }
        // else z = 0 (default value)
    }
}

contract TestSafeMath {
  	// å¯¹uintç±»å‹å¢åŠ SafeMathçš„æ–¹æ³•ï¼Œ
  	// 1. åç»­å®šä¹‰çš„uintå˜é‡å°±ä¼šè‡ªåŠ¨ç»‘å®šSafeMathæä¾›çš„æ–¹æ³•: uint x;
  	// 2. è¿™ä¸ªå˜é‡ä¼šä½œä¸ºç¬¬ä¸€ä¸ªå‚æ•°ä¼ é€’ç»™å‡½æ•°: x.add(y);
    using SafeMath for uint;

    uint public MAX_UINT = 2**256 - 1;
		
  	// ç”¨æ³•1ï¼šx.æ–¹æ³•(y)
    function testAdd(uint x, uint y) public pure returns (uint) {
       //return x.add(y);
      return SafeMath.add(x,y);
    }

  	// ç”¨æ³•2ï¼šåº“.æ–¹æ³•(x)
    function testSquareRoot(uint x) public pure returns (uint) {
        return Math.sqrt(x);
    }
}

// 2. å­˜åœ¨publicæ–¹æ³•æ—¶ï¼Œä¼šå•ç‹¬éƒ¨ç½²åº“åˆçº¦ï¼Œå¹¶ä¸”ç¬¬ä¸€ä¸ªå‚æ•°æ˜¯çŠ¶æ€å˜é‡ç±»å‹
library Array {
  	// ä¿®æ”¹è°ƒç”¨è€…çŠ¶æ€å˜é‡çš„æ–¹å¼ï¼Œç¬¬ä¸€ä¸ªå‚æ•°æ˜¯çŠ¶æ€å˜é‡æœ¬èº«
    function remove(uint[] storage arr, uint index) public {
        // Move the last element into the place to delete
        require(arr.length > 0, "Can't remove from empty array");
        arr[index] = arr[arr.length - 1];
        arr.pop();
    }
}

contract TestArray {
    using Array for uint[];

    uint[] public arr;

    function testArrayRemove() public {
        for (uint i = 0; i < 3; i++) {
            arr.push(i);
        }

        arr.remove(1);

        assert(arr.length == 2);
        assert(arr[0] == 0);
        assert(arr[1] == 2);
    }
}
```



## encode

1. abi.**encode**ï¼šå¯ä»¥å°†dataç¼–ç æˆbytesï¼Œç”Ÿæˆçš„bytesæ€»æ˜¯32å­—èŠ‚çš„å€æ•°ï¼Œä¸è¶³32ä¸ºä¼šè‡ªåŠ¨å¡«å……ï¼ˆç”¨äºç»™åˆçº¦è°ƒç”¨ï¼‰ï¼›
2. abi.**decode**ï¼šå¯ä»¥å°†bytesè§£ç æˆdataï¼ˆå¯ä»¥åªè§£æéƒ¨åˆ†å­—æ®µï¼‰
3. abi.**encodePacked**ï¼šä¸abi.encodeç±»ä¼¼ï¼Œä½†æ˜¯ç”Ÿæˆçš„bytesæ˜¯å‹ç¼©è¿‡çš„ï¼ˆæœ‰äº›ç±»å‹ä¸ä¼šè‡ªåŠ¨å¡«å……ï¼Œæ— æ³•ä¼ é€’ç»™åˆçº¦è°ƒç”¨ï¼‰ã€‚
4. æ‰‹å†Œï¼šhttps://docs.soliditylang.org/en/v0.8.13/abi-spec.html?highlight=abi.encodePacked#non-standard-packed-mode

```js
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.13;

contract AbiDecode {
    struct MyStruct {
        string name;
        uint[2] nums;
    }

    // input: 10, 0x5B38Da6a701c568545dCfcB03FcB875f56beddC4, [1,"2",3], ["duke", [10,20]]
    // output: 0x000000000000000000000000000000000000000000000000000000000000000a0000000000000000000000005b38da6a701c568545dcfcb03fcb875f56beddc40000000000000000000000000000000000000000000000000000000000000080000000000000000000000000000000000000000000000000000000000000010000000000000000000000000000000000000000000000000000000000000000030000000000000000000000000000000000000000000000000000000000000001000000000000000000000000000000000000000000000000000000000000000200000000000000000000000000000000000000000000000000000000000000030000000000000000000000000000000000000000000000000000000000000060000000000000000000000000000000000000000000000000000000000000000a0000000000000000000000000000000000000000000000000000000000000014000000000000000000000000000000000000000000000000000000000000000064756b6500000000000000000000000000000000000000000000000000000000
    //  outputé•¿åº¦ï¼š832ä½16è¿›åˆ¶å­—ç¬¦ï¼ˆå»é™¤0x)ï¼Œ832 / 32 = 26 ï¼ˆä¸€å®šæ˜¯32å­—èŠ‚çš„æ•´æ•°å€ï¼Œä¸è¶³å¡«0ï¼‰
    function encode(
        uint x,
        address addr,
        uint[] calldata arr,
        MyStruct calldata myStruct
    ) external pure returns (bytes memory) {
        return abi.encode(x, addr, arr, myStruct);
    }

    function decode(bytes calldata data)
        external
        pure
        returns (
            uint x,
            address addr,
            uint[] memory arr,
            MyStruct memory myStruct
        )
    {
        (x, addr, arr, myStruct) = abi.decode(data, (uint, address, uint[], MyStruct));

        /* decode output: 
            0: uint256: x 10
            1: address: addr 0x5B38Da6a701c568545dCfcB03FcB875f56beddC4
            2: uint256[]: arr 1,2,3
            3: tuple(string,uint256[2]): myStruct ,10,20
        */
    }

    // å¯ä»¥åªdecodeå…¶ä¸­éƒ¨åˆ†å­—æ®µï¼Œè€Œä¸ç”¨å…¨éƒ¨decodeï¼Œå½“å‰æ¡ˆä¾‹ä¸­ï¼Œåªæœ‰ç¬¬ä¸€ä¸ªå­—æ®µè¢«è§£æäº†ï¼Œå…¶ä½™ä¸ºé»˜è®¤å€¼
    function decodeLess(bytes calldata data)
        external
        pure
        returns (
            uint x,
            address addr,
            uint[] memory arr,
            MyStruct memory myStruct
        )
    {
        (x) = abi.decode(data, (uint));

        /* decode output: 
            0: uint256: x 10
            1: address: addr 0x0000000000000000000000000000000000000000
            2: uint256[]: arr
            3: tuple(string,uint256[2]): myStruct ,0,0
        */
    }

    // input: -1, 0x42, 0x03, "Hello, world!"
    function encodePacked(
        int16 x,
        bytes1 y,
        uint16 z,
        string memory s
    ) external view returns (bytes memory) {

        // encodePacked ä¸æ”¯æŒstructå’Œmapping
        return abi.encodePacked(x, y, z, s);

        /*
        0xffff42000348656c6c6f2c20776f726c6421
          ^^^^                                 int16(-1)
              ^^                               bytes1(0x42)
                ^^^^                           uint16(0x03)
                    ^^^^^^^^^^^^^^^^^^^^^^^^^^ string("Hello, world!") without a length field
        */
    }
  
  	// å¯ä»¥ç”¨encodePackedæ¥æ‹¼æ¥å­—ç¬¦ä¸²
  	// output string: ipfs://bafybeidmrsvehl4ehipm5qqvgegi33r6/100.json
  	function encodePackedTest() public  pure returns (string memory) {
        string memory uri = "ipfs://bafybeidmrsvehl4ehipm5qqvgegi33r6/";
        return string(abi.encodePacked(uri, "100", ".json"));
    }
}
```

## keccak256

keccak256ç”¨äºè®¡ç®—å“ˆå¸Œï¼Œå±äºsha3ç®—æ³•ï¼Œä¸sha256ï¼ˆå±äºsha2ç®—æ³•ä¸åŒï¼‰ï¼Œkeccak256ä½¿ç”¨åœºæ™¯å¦‚ä¸‹ï¼š

1. ç”¨äºç”Ÿæˆå”¯ä¸€idï¼›
2. ç”Ÿæˆæ•°æ®æŒ‡çº¹ï¼›

```js
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.13;

contract HashFunction {
    function hash(
        string memory _text,
        uint _num,
        address _addr
    ) public pure returns (bytes32) {
        return keccak256(abi.encodePacked(_text, _num, _addr));
    }

    // Example of hash collision
    // Hash collision can occur when you pass more than one dynamic data type
    // to abi.encodePacked. In such case, you should use abi.encode instead.
    function collision(string memory _text, string memory _anotherText)
        public
        pure
        returns (bytes32)
    {
        // encodePacked(AAA, BBB) -> AAABBB
        // encodePacked(AA, ABBB) -> AAABBB
        return keccak256(abi.encodePacked(_text, _anotherText));
    }
}

contract GuessTheMagicWord {
    bytes32 public answer =
        0x60298f78cc0b47170ba79c10aa3851d7648bd96f2f8e46a19dbc777c36fb0c00;

    // Magic word is "Solidity"
    function guess(string memory _word) public view returns (bool) {
        return keccak256(abi.encodePacked(_word)) == answer;
    }
}
```



## Send Ether

**å¦‚ä½•å‘é€etherï¼Ÿ**

æœ‰ä¸‰ç§æ–¹å¼å¯ä»¥å‘åˆçº¦åœ°å€è½¬etherï¼š

1. ~~sendï¼ˆ21000 gasï¼Œreturn boolï¼‰~~
2. transferï¼ˆ21000 gasï¼Œ throw errorï¼‰
3. callï¼ˆä¼ é€’äº¤æ˜“å‰©ä½™çš„gasæˆ–è®¾ç½®gasï¼Œä¸é™å®š21000gasï¼Œreturn boolï¼‰(æ¨èä½¿ç”¨)

æ€»ç»“ï¼štransfer() å’Œ send() å‡½æ•°ä½¿ç”¨ 2300 gas ä»¥é˜²æ­¢é‡å…¥æ”»å‡»ï¼Œä½†å…¬é“¾å‡çº§åå¯èƒ½å¯¼è‡´ gas ä¸è¶³ã€‚æ‰€ä»¥æ¨èä½¿ç”¨ call() å‡½æ•°ï¼Œä½†éœ€åšå¥½é‡å…¥æ”»å‡»é˜²æŠ¤ã€‚



**å¦‚ä½•æ¥æ”¶etherï¼Ÿ**

æƒ³æ¥æ”¶etherçš„åˆçº¦è‡³å°‘åŒ…å«ä»¥ä¸‹æ–¹æ³•ä¸­çš„ä¸€ä¸ªï¼š

1. receive() external payableï¼šmsg.dataä¸ºç©ºæ—¶è°ƒç”¨ï¼ˆä¸ºæ¥æ”¶etherè€Œç”Ÿï¼Œä»…solidity 0.6ç‰ˆæœ¬ä¹‹å)
2. fallback() external payableï¼šmsg.dataéç©ºæ—¶è°ƒç”¨ï¼ˆä¸ºæ‰§è¡Œdefaulté€»è¾‘è€Œç”Ÿï¼Œ**é¡ºä¾¿æ”¯æŒæ¥æ”¶ether**ï¼‰

```js
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.13;

contract ReceiveEther {
    /*
    Which function is called, fallback() or receive()?

                sender ether
                    |
             msg.data is empty?
                /       \
            yes          no
             /             \
      receive() exist?     fallback()
          /    \
        yes     no
       /          \
  receive()     fallback()
  */

    string public message;

    // Function to receive Ether. msg.data must be empty
    receive() external payable {
        message = "receive called!";
    }

    // Fallback function is called when msg.data is not empty
    fallback() external payable {
        message = "fallback called!";
    }

    function getBalance() public view returns (uint) {
        return address(this).balance;
    }

    function setMsg(string memory _msg) public {
        message = _msg;
    }
}

contract SendEther {
    function sendViaTransfer(address payable _to) public payable {
        // This function is no longer recommended for sending Ether. (ä¸å»ºè®®ä½¿ç”¨)
        _to.transfer(msg.value);
    }

    function sendViaSend(address payable _to) public payable {
        // Send returns a boolean value indicating success or failure.
        // This function is not recommended for sending Ether. (ä¸å»ºè®®ä½¿ç”¨)
        bool sent = _to.send(msg.value);
        require(sent, "Failed to send Ether");
    }

    function sendViaCallFallback(address payable _to) public payable {
        // Call returns a boolean value indicating success or failure.
        // This is the current recommended method to use. (æ¨èä½¿ç”¨)
        (bool sent, bytes memory data) = _to.call{value: msg.value}(abi.encodeWithSignature("noExistFuncTest()"));
        require(sent, "Failed to send Ether");
    }

    function sendViaCallReceive(address payable _to) public payable {
        // Call returns a boolean value indicating success or failure.
        // This is the current recommended method to use.(æ¨èä½¿ç”¨)
        (bool sent, bytes memory data) = _to.call{value: msg.value}("");
        require(sent, "Failed to send Ether");
    }
}
```

è§£æï¼š

- è°ƒç”¨sendViaTransferæˆ–sendViaSendçš„æ—¶å€™ï¼Œå‡è®¾æ„é€ è¿™ç¬”äº¤æ˜“æ—¶ï¼Œä½ ä¼ å…¥çš„gasæ—¶ï¼š1000000 gas

  æ­¤æ—¶ï¼Œåœ¨ä½¿ç”¨transferå’Œsendè½¬è´¦çš„æ—¶å€™ï¼Œåªä¼šä¼ é€’2300ä¸ªgasï¼Œå¦‚æœæ¥æ”¶è€…æ˜¯ä¸ªåˆçº¦ï¼Œè¿™ä¸ªåˆçº¦å¿…é¡»æœ‰fallbackï¼Œæ­¤æ—¶è¿™ä¸ªfallbacké‡Œé¢ä¸èƒ½æœ‰é€»è¾‘ï¼Œå¦åˆ™ä¼šè¶…è¿‡2300gasï¼Œå¯¼è‡´è½¬è´¦å¤±è´¥ã€‚

- sendViaCallçš„æ—¶å€™ï¼Œå‡è®¾æ„é€ è¿™ç¬”äº¤æ˜“æ—¶ï¼Œä½ ä¼ å…¥çš„gasæ—¶ï¼š1000000 gas
  æ­¤æ—¶åœ¨è°ƒç”¨callçš„æ—¶å€™ï¼Œä¹Ÿå¯ä»¥å®Œæˆè½¬è´¦ï¼Œä½†æ˜¯ä¼šæŠŠ1000000ä¼ é€’ç»™fallbackï¼Œå³åœ¨fallbackä¸­ä½ å¯ä»¥å®ç°è‡ªå·±å¤æ‚çš„é€»è¾‘ã€‚

- å‚è€ƒé“¾æ¥ï¼šhttps://docs.soliditylang.org/en/latest/security-considerations.html#sending-and-receiving-ether



## call

- **call**æ˜¯ä¸€ç§åº•å±‚è°ƒç”¨åˆçº¦çš„æ–¹å¼ï¼Œå¯ä»¥åœ¨åˆçº¦å†…è°ƒç”¨å…¶ä»–åˆçº¦

- å½“è°ƒç”¨fallbackæ–¹å¼ç»™åˆçº¦è½¬etherçš„æ—¶å€™ï¼Œ**å»ºè®®ä½¿ç”¨call**ï¼Œè€Œä¸æ˜¯ä½¿ç”¨transferæˆ–sendæ–¹æ³•

- å¯¹äºå­˜åœ¨çš„æ–¹æ³•ï¼Œä¸å»ºè®®ä½¿ç”¨callæ–¹å¼è°ƒç”¨

- è°ƒç”¨ä¸å­˜åœ¨çš„æ–¹æ³•ï¼ˆåˆä¸å­˜åœ¨fallbackï¼‰æ—¶ï¼Œäº¤æ˜“ä¼šè°ƒç”¨æˆåŠŸï¼Œä½†æ˜¯ç¬¬ä¸€ä¸ªå‚æ•°ä¸ºï¼šfalseï¼Œæ‰€ä»¥ä½¿ç”¨callè°ƒç”¨åä¸€å®šè¦æ£€æŸ¥successçŠ¶æ€

```js
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.13;

contract Receiver {
    event Received(address caller, uint amount, string message);

    fallback() external payable {
        emit Received(msg.sender, msg.value, "Fallback was called");
    }

    function foo(string memory _message, uint _x) public payable returns (uint) {
        emit Received(msg.sender, msg.value, _message);

        return _x + 1;
    }
}

contract Caller {
    event Response(bool success, bytes data);

    function testCallFoo(address payable _addr) public payable {
        // You can send ether and specify a custom gas amount
        (bool success, bytes memory data) = _addr.call{value: msg.value, gas: 5000}(
            abi.encodeWithSignature("foo(string,uint256)", "call foo", 123)
        );

        emit Response(success, data);
    }

    // Calling a function that does not exist triggers the fallback function.
    function testCallDoesNotExist(address _addr) public {
        (bool success, bytes memory data) = _addr.call(
            abi.encodeWithSignature("doesNotExist()")
        );

        emit Response(success, data);
    }
}
```

## staticcall

- https://eips.ethereum.org/EIPS/eip-214
- Since byzantium staticcall can be used as well. This is basically the same as call, but will revert if the called function modifies the state in any way.
- ä¸CALLç›¸åŒï¼Œä½†æ˜¯ä¸å…è®¸ä¿®æ”¹ä»»ä½•çŠ¶æ€å˜é‡ï¼Œæ˜¯ä¸ºäº†å®‰å…¨ğŸ”è€ƒè™‘è€Œæ–°å¢çš„OPCODE
- åœ¨Transparentæ¨¡å¼çš„ä»£ç†åˆçº¦é€»è¾‘ä¸­ï¼Œå°±ä½¿ç”¨äº†staticcallï¼Œä»è€Œè®©proxyAminèƒ½å¤Ÿå…è´¹çš„è°ƒç”¨çˆ¶åˆçº¦çš„adminå‡½æ•°ï¼Œä»è€Œä»slotä¸­è¿”å›ä»£ç†åˆçº¦çš„ç®¡ç†å‘˜ã€‚è¿™éƒ¨åˆ†ä¼šåœ¨åˆçº¦å‡çº§ç« èŠ‚ä»‹ç»ã€‚

```js
    function getProxyAdmin(TransparentUpgradeableProxy proxy) public view virtual returns (address) {
        // We need to manually run the static call since the getter cannot be flagged as view
        // bytes4(keccak256("admin()")) == 0xf851a440
        (bool success, bytes memory returndata) = address(proxy).staticcall(hex"f851a440");
        require(success);
        return abi.decode(returndata, (address));
    }
```



## delegatecall

delegatecallä¸callç›¸ä¼¼ï¼Œä¹Ÿæ˜¯åº•å±‚è°ƒç”¨åˆçº¦æ–¹å¼ï¼Œç‰¹ç‚¹æ˜¯ï¼š

1. å½“Aåˆçº¦ä½¿ç”¨delegatecallè°ƒç”¨Båˆçº¦çš„æ–¹æ³•æ—¶ï¼ŒBåˆçº¦çš„ä»£ç è¢«æ‰§è¡Œï¼Œä½†æ˜¯**ä½¿ç”¨çš„æ˜¯Aåˆçº¦çš„ä¸Šä¸‹æ–‡**ï¼ŒåŒ…æ‹¬Aåˆçº¦çš„çŠ¶æ€å˜é‡ï¼Œmsg.senderï¼Œmsg.valueç­‰ï¼›
2. ä½¿ç”¨delegatecallçš„å‰ææ˜¯ï¼šAåˆçº¦å’ŒBåˆçº¦æœ‰ç›¸åŒçš„çŠ¶æ€å˜é‡ã€‚

![image-20220510094354223](https://duke-typora.s3.amazonaws.com/ipic/2023-03-06-085436.png)

```js
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.13;

contract Implementation {
    // NOTE: storage layout must be the same as contract A
    uint public num;
    address public sender;
    uint public value;

    function setVars(uint _num) public payable {
        num = _num;
        sender = msg.sender;
        value = msg.value;
    }
}

contract ImplementationV2 {
    // NOTE: storage layout must be the same as contract A
    uint public num;
    address public sender;
    uint public value;

    function setVars(uint _num) public payable {
        num = _num*2;
        sender = msg.sender;
        value = msg.value;
    }
}

// æ³¨æ„ï¼šæ‰§è¡Œåï¼ŒProxyä¸­çš„senderå€¼ä¸ºEOAçš„åœ°å€ï¼Œè€Œä¸æ˜¯Aåˆçº¦çš„åœ°å€  (è°ƒç”¨é“¾EOA-> Proxy::setVars -> Implementation::setVars)
contract Proxy {
    uint public num;
    address public sender;
    uint public value;

    function setVars(address _impl, uint _num) public payable {
        // Proxy's storage is set, Implementation is not modified.
        (bool success, bytes memory data) = _impl.delegatecall(
            abi.encodeWithSignature("setVars(uint256)", _num)
        );
    }
}
```



## create&create2

åˆ›å»ºåˆçº¦æ—¶ï¼Œåœ¨ä¸–ç•ŒçŠ¶æ€ä¸­ï¼Œå¢åŠ ä¸€ä¸ªåœ°å€ä¸è´¦æˆ·çš„ä¿¡æ¯ã€‚

![image-20220906214046327](https://duke-typora.s3.amazonaws.com/ipic/2023-03-06-085757.png)

åœ¨EVMå±‚é¢ï¼Œä¸€å…±æœ‰ä¸¤ä¸ªæ“ä½œç ï¼ˆOPCODEï¼‰å¯ä»¥ç”¨æ¥åˆ›å»ºåˆçº¦ï¼š

1. createï¼š

- åŸç†ï¼šæ–°ç”Ÿæˆåœ°å€ = hash(åˆ›å»ºè€…åœ°å€, nonce)
- ç‰¹ç‚¹ï¼šä¸å¯é¢„æµ‹ï¼Œå› ä¸ºnonceæ˜¯å˜åŒ–çš„

2. create2ï¼š

- åŸç†ï¼šæ–°ç”Ÿæˆåœ°å€ = hash("0xFF",åˆ›å»ºè€…åœ°å€, salt, bytecodeHash)
- ç‰¹ç‚¹ï¼šå¯ä»¥é¢„æµ‹ï¼Œå› ä¸ºæ²¡æœ‰å˜é‡

åœ¨ç¼–ç æ—¶ï¼Œæˆ‘ä»¬å¯ä»¥ç›´æ¥ä½¿ç”¨æ±‡ç¼–æ¥åˆ›å»ºæ–°åˆçº¦ï¼Œä¹Ÿå¯ä»¥ä½¿ç”¨solidityä¸­çš„newå…³é”®å­—æ¥åˆ›å»ºæ–°åˆçº¦ï¼š

1. ä½¿ç”¨æ±‡ç¼–æ–¹å¼ï¼š

```js
assembly {
  create(å‚æ•°...)
}

assembly {
  create2(å‚æ•°...)
}
```

2. ä½¿ç”¨newæ–¹å¼åˆ›å»ºï¼š

```js
// å†…éƒ¨è°ƒç”¨create
new ContractName(å‚æ•°...)

// å†…éƒ¨è°ƒç”¨create2
// åœ¨0.8.0ç‰ˆæœ¬ä¹‹åï¼Œnewå¢åŠ äº†salté€‰é¡¹ï¼Œä»è€Œæ”¯æŒäº†create2çš„ç‰¹æ€§ï¼ˆé€šè¿‡saltå¯ä»¥è®¡ç®—å‡ºåˆ›å»ºåˆçº¦çš„åœ°å€ï¼‰ã€‚
new ContractName{salt: _salt}(å‚æ•°...)
```

demoéªŒè¯ï¼š

```js
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.13;

contract Car {
    address public owner;
    string public model;
    address public carAddr;

    constructor(address _owner, string memory _model) payable {
        owner = _owner;
        model = _model;
        carAddr = address(this);
    }
}

contract CarFactory {
    Car[] public cars;

    function create(address _owner, string memory _model) public {
        Car car = new Car(_owner, _model);
        cars.push(car);
    }

    function createAndSendEther(address _owner, string memory _model) public payable {
        Car car = (new Car){value: msg.value}(_owner, _model);
        cars.push(car);
    }

    function create2(
        address _owner,
        string memory _model,
        bytes32 _salt
    ) public {
        Car car = (new Car){salt: _salt}(_owner, _model);
        cars.push(car);
    }

    function create2AndSendEther(
        address _owner,
        string memory _model,
        bytes32 _salt
    ) public payable {
        Car car = (new Car){value: msg.value, salt: _salt}(_owner, _model);
        cars.push(car);
    }

    function getCar(uint _index)
        public
        view
        returns (
            address owner,
            string memory model,
            address carAddr,
            uint balance
        )
    {
        Car car = cars[_index];

        return (car.owner(), car.model(), car.carAddr(), address(car).balance);
    }
}
```



## åˆçº¦é—´è°ƒç”¨

æ™®é€šçš„äº¤æ˜“ï¼Œç›¸å½“äºåœ¨ä¸–ç•ŒçŠ¶æ€ä¸­ä¿®æ”¹åŸæœ‰çš„è´¦æˆ·æ•°æ®ï¼Œæ›´æ–°åˆ°æ–°çŠ¶æ€ã€‚

![image-20220906214200031](https://duke-typora.s3.amazonaws.com/ipic/2023-03-06-090931.png)

ä¸€å…±æœ‰ä¸‰ç§æ–¹å¼è°ƒç”¨åˆçº¦ï¼š

1. ä½¿ç”¨åˆçº¦å®ä¾‹è°ƒç”¨åˆçº¦ï¼ˆå¸¸è§„ï¼‰ï¼š**A.foo(argument)**
2. ä½¿ç”¨callè°ƒç”¨åˆçº¦: **A.call(calldata)**
3. ä½¿ç”¨delegateè°ƒç”¨åˆçº¦ï¼š**A.delegatecall(calldata)**

```js
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.13;

contract Callee {
    uint public x;
    uint public value;

    function setX(uint _x) public returns (uint) {
        x = _x;
        return x;
    }

    function setXandSendEther(uint _x) public payable returns (uint, uint) {
        x = _x;
        value = msg.value;

        return (x, value);
    }
}

contract Caller {
    // ç›´æ¥åœ¨å‚æ•°ä¸­è¿›è¡Œå®ä¾‹åŒ–åˆçº¦
    function setX(Callee _callee, uint _x) public {
        uint x = _callee.setX(_x);
    }

    // ä¼ é€’åœ°å€ï¼Œåœ¨å†…éƒ¨å®ä¾‹åŒ–calleeåˆçº¦
    function setXFromAddress(address _addr, uint _x) public {
        Callee callee = Callee(_addr);
        callee.setX(_x);
    }

    // è°ƒç”¨æ–¹æ³•ï¼Œå¹¶è½¬ether
    function setXandSendEther(Callee _callee, uint _x) public payable {
        (uint x, uint value) = _callee.setXandSendEther{value: msg.value}(_x);
    }
}

```



## uniswapV2

![image-20230307193843382](https://duke-typora.s3.amazonaws.com/ipic/2023-03-07-113843.png)
